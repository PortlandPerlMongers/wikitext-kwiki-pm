#!/usr/bin/perl

use strict; use warnings;
use IO::All;
use WikiText::Kwiki::Parser;
use WikiText::Markdown::Emitter;
use XXX;

run(@ARGV);

sub run {
    my $input_dir = shift
        or die usage("Missing input directory");
    -d $input_dir
        or die usage("'$input_dir' not a directory");
    my $output_dir = shift
        or die usage("Missing output directory");
    -d $output_dir
        or die usage("'$output_dir' not a directory");
    die usage("Unknown arguments: '@_'")
        if @_;
    die usage("Output directory must be empty")
        unless io->dir($output_dir)->empty;

    for my $kwiki_file (io->dir($input_dir)->all_files) {
        my $markdown = convert_kwiki_file(io($kwiki_file)->all);
        $markdown =~ s/\n\n\z/\n/;  # Remove extra final blank line.
        my $name = $kwiki_file->filename;
        io->file("$output_dir/$name.md")->print($markdown);
    }
}

sub convert_kwiki_file {
    my ($input) = @_;
    require WikiText::HTML::Emitter;
    my $parser = WikiText::Kwiki::Parser->new(
        receiver => WikiText::Markdown::Emitter->new(
            list_stack => [],
            list_depth => 0,
            list_count => 0,
        ),
    );
    return $parser->parse($input);
}

sub usage {
    my $msg = shift || '';
    my $usage = <<'...';
Usage:
    kwiki2markdown <kwiki/input/dir> <markdown/output/dir>

Note: output directory must be empty.
...
    $usage = "Error: '$msg'\n\n$usage" if $msg;
    return $usage;
}
